import org.gradle.nativeplatform.platform.internal.DefaultNativePlatform

plugins {
    id 'java'
    id 'application'
    // use for javafx support
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

sourceCompatibility = 17
targetCompatibility = 17

def mainClassName = "toe.tic.tac.Main"

application {
    mainClass.set(mainClassName)
}

group 'tic.tac'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    //implementation files('libs/some_local_library_jar_file.jar')
}

// Use for JavaFX project:
javafx {
    version = '17'
    modules = ['javafx.controls', 'javafx.fxml']
}

test {
    useJUnitPlatform()
}

task javaFxJar(type: Jar) {
    dependsOn compileJava
    //destinationDirectory = file("${rootDir}/releases")
    OperatingSystem operatingSystem = DefaultNativePlatform.currentOperatingSystem;
    if (operatingSystem.isWindows()) {
        archiveBaseName = "TicTacToeFX-win"
    } else if (operatingSystem.isMacOsX()) {
        archiveBaseName = "TicTacToeFX-osx"
    } else if (operatingSystem.isLinux()) {
        archiveBaseName = "TicTacToeFX-linux"
    } else {
        archiveBaseName = "TicTacToeFX"
    }

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    from(sourceSets.main.output) {
        include "**"
        //exclude "**/TestServerClients.class"
        //exclude "**/client/Client.class"
        //exclude "**/view/*"
    }

    manifest {
        // a non-javafx main class is needed for jars
        attributes 'Main-Class': mainClassName
    }
}